AC_INIT
AM_INIT_AUTOMAKE(barvinok, 0.18)
AC_PROG_CXX
AM_PROG_LEX
AC_PROG_YACC
AC_SUBST(versioninfo)
versioninfo=10:0:0

AC_CANONICAL_HOST
AC_LIBTOOL_WIN32_DLL
AC_SUBST(BV_LDFLAGS)
case $host in
	*-*-cygwin* | *-*-mingw* | *-*-pw32* | *-*-os2*)
	if test "$enable_shared" = yes; then
		BV_LDFLAGS="-no-undefined" 
	fi
	;;
esac

AC_PROG_LIBTOOL

AC_CHECK_HEADERS(getopt.h)
AC_CHECK_HEADERS(sys/times.h)

dnl Check for GMP library
AC_MSG_CHECKING(whether to use GMP)
AC_ARG_WITH(libgmp, 
        [  --with-libgmp           DIR Location of the GMP Distribution], 
	gmp_package=$withval, gmp_package=yes)
if test "x$gmp_package" = "xno"; then
	AC_MSG_RESULT(no)
	AC_MSG_ERROR(Need gmp)
else
	AC_MSG_RESULT(yes)
	if test "x$gmp_package" != "xyes"; then
	    GMP_DIR=$gmp_package
	    if test ! -d "$GMP_DIR"; then
	    	AC_ERROR(Directory given for GMP Distribution is not a directory)
	    fi
	    CPPFLAGS="-I$GMP_DIR/include $CPPFLAGS"
	    LDFLAGS="-L$GMP_DIR/lib $LDFLAGS"
	fi
	poly_cv_gmpfatal="no"
	AC_CHECK_HEADER(gmp.h,
		[AC_CHECK_LIB(gmp,main,
		    [],
		    [poly_cv_gmpfatal="yes"])],
		[poly_cv_gmpfatal="yes"])
	if test "$poly_cv_gmpfatal" = "yes"; then
		AC_MSG_ERROR([GMP not found])
	fi
fi
AC_CHECK_DECL(mpz_divisible_p,[],[AC_LIBOBJ(mpz_divisible_p)],[#include <gmp.h>])

AC_ARG_WITH(polylib,
            [  --with-polylib=DIR      DIR Location of PolyLib],
            [ echo "Package polylib : $withval" && polylib_package=$withval],  
            [ polylib_package=yes ])

if test "$polylib_package" = "no"; then
    AC_MSG_ERROR(Need polylib)
fi

if test "$polylib_package" != "yes"; then
    CPPFLAGS="-I$polylib_package/include $CPPFLAGS"
    LDFLAGS="-L$polylib_package/lib $LDFLAGS"
    LD_LIBRARY_PATH="$polylib_package/lib:$LD_LIBRARY_PATH"
    export LD_LIBRARY_PATH
fi

AC_CHECK_LIB(polylibgmp, PolyhedronTSort,[],[
    AC_MSG_ERROR(Need polylib)
])
AC_CHECK_FUNCS(Enumeration_Free)
AC_SUBST(bv_extra_programs)
AC_CHECK_FUNCS(LexSmaller,[
	bv_extra_programs="verify_lexsmaller $bv_extra_programs"
    ])
AC_REPLACE_FUNCS(DomainConstraintSimplify)
AC_TRY_RUN([
#include <polylib/polylibgmp.h>

int main()
{
    int i, j;
    Polyhedron *P;
    Matrix *M = Matrix_Alloc(6,5);
    for (i = 0; i < 6; ++i) {
	value_set_si(M->p[i][0], 1);
	for (j = 1; j < 4; ++j)
	    value_set_si(M->p[i][j], (j-1 == i/2) * (1-2*(i%2)));
	value_set_si(M->p[i][4], i%2);
    }
    P = Constraints2Polyhedron(M, 6);
    return !(P->NbRays == 8);
}
],
[AC_DEFINE(HAVE_GROWING_CHERNIKOVA,[],
	  [PolyLib automatically grows the Chernikova table space])],
AC_MSG_WARN([This version of PolyLib does not automatically])
AC_MSG_WARN([enlarge the Chernikova table space]),
AC_MSG_WARN([Unable to determine whether PolyLib automatically])
AC_MSG_WARN([grows the Chernikova table space]))

AC_TRY_RUN([
#include <polylib/polylibgmp.h>

int main()
{
    enode *en = new_enode(polynomial, 1, 1);
    return !(en->arr[0].x.p == NULL);
}
],,
AC_MSG_WARN([This version of polylib leaks]),[true])

AC_TRY_RUN([
#include <stdio.h>
#include <polylib/polylibgmp.h>

int main()
{
    Matrix *M;
    Polyhedron *P, *C;
    Param_Polyhedron *PP;
    int i;
    Param_Vertices *V;

    freopen("$srcdir/tests/ehrhart/t12.in", "r", stdin);
    M = Matrix_Read();
    P = Constraints2Polyhedron(M, 1024);
    M = Matrix_Read();
    C = Constraints2Polyhedron(M, 1024);
    PP = Polyhedron2Param_SimplifiedDomain(&P, C, 1024, NULL, NULL);
    for (i = 0, V = PP->V; V; ++i, V = V->next)
	;
    return !(i == 10);
}
],
[AC_DEFINE(HAVE_CORRECT_VERTICES,[],
	  [PolyLib computes correct vertices])],
AC_MSG_WARN([This version of polylib produces incorrect vertices.]),
AC_MSG_WARN([Unable to determine whether PolyLib computes correct vertices]))

AC_MSG_CHECKING([number of arguments of Polyhedron_Enumerate])
AC_TRY_COMPILE([#include <polylib/polylibgmp.h>], 
	       [Polyhedron_Enumerate(NULL,NULL,0,NULL)],
	       [AC_MSG_RESULT(4)
		AC_DEFINE(HAVE_ENUMERATE4,[],
			  [Polyhedron_Enumerate takes four arguments])],
	       [AC_MSG_RESULT(3)])

AC_MSG_CHECKING([number of arguments of count_points])
AC_TRY_COMPILE([#include <polylib/polylibgmp.h>], 
	       [count_points(0,NULL,NULL,NULL)],
	       [AC_MSG_RESULT(4)
		AC_DEFINE(HAVE_COUNT_POINTS4,[],
			  [count_points takes four arguments])],
	       [AC_MSG_RESULT(3)])

AC_MSG_CHECKING([for value_subtract macro])
AC_EGREP_CPP(yes, [
    #include <polylib/polylibgmp.h>
    #ifdef value_subtract
    yes
    #endif
    ],[AC_MSG_RESULT(yes)],
    [AC_MSG_RESULT(no)
     AC_DEFINE(value_subtract,value_substract,
		[define to value_substract if missing from PolyLib])])

AC_ARG_WITH(piplib,
            [  --with-piplib=DIR       DIR Location of PIP],
            [ echo "Package piplib : $withval" && piplib_package=$withval],  
            [ piplib_package=yes ])

if test "$piplib_package" != "no"; then
    if test "$piplib_package" != "yes"; then
	CPPFLAGS="-I$piplib_package/include $CPPFLAGS"
	LDFLAGS="-L$piplib_package/lib $LDFLAGS"
    fi

    AC_CHECK_LIB(piplibMP, pip_solve,[
	AC_DEFINE(HAVE_PIPLIB,[],[use piplib])
	LIBS="-lpiplibMP $LIBS"
	AC_LIBOBJ(piputil)
	bv_extra_programs="piptest $bv_extra_programs"
    ],[
	AC_MSG_WARN([Piplib not found])
    ])
fi

AC_MSG_CHECKING(whether to use Omega)
AC_ARG_WITH(omega, [AS_HELP_STRING([--with-omega=DIR],[DIR Location of Omega])],
            [ omega_package=$withval],  [ omega_package=yes ])
AC_MSG_RESULT($omega_package)

AC_SUBST(OMEGA_CPPFLAGS)
AC_SUBST(OMEGA_LDFLAGS)
AC_SUBST(bv_omega_programs)
if test "$omega_package" != "no"; then
    if test "x$omega_package" != "xyes"; then
	OMEGA_CPPFLAGS="-I$omega_package/include/omega $CPPFLAGS"
	OMEGA_LDFLAGS="-L$omega_package/lib $LDFLAGS"
    else
	for i in /usr/include /usr/local/include; do
	    if test -f $i/omega/omega.h; then
		OMEGA_CPPFLAGS="-I$i/omega $CPPFLAGS"
	    fi
	done
    fi

    AC_LANG_PUSH(C++)
    SAVE_CPPFLAGS="$CPPFLAGS"
    CPPFLAGS="$OMEGA_CPPFLAGS"
    AC_CHECK_HEADERS([omega.h],[
	bv_omega_programs="occ $bv_omega_programs"
    ],[AC_MSG_WARN(Omega not found)])
    CPPFLAGS="$SAVE_CPPFLAGS"
    AC_LANG_POP
fi

AC_MSG_CHECKING(if --enable-fractional option specified)
AC_ARG_ENABLE(fractional,
            [  --enable-fractional     Use fractional representation],
	    [bv_cv_fractional=$enableval], [bv_cv_fractional="no"])
AC_MSG_RESULT($bv_cv_fractional)
AC_MSG_CHECKING(if --enable-modulo option specified)
AC_ARG_ENABLE(modulo,
            [  --enable-modulo         Use fractional representation],
	    [bv_cv_modulo=$enableval], [bv_cv_modulo="no"])
AC_MSG_RESULT($bv_cv_modulo)
if test "x$bv_cv_modulo" != "xno" -o "x$bv_cv_fractional" != "xno"; then
        AC_DEFINE(USE_MODULO,[], [Use fractional representation])
fi

AC_MSG_CHECKING(whether to use incremental algorithm)
AC_ARG_ENABLE(incremental,
	[AS_HELP_STRING([--enable-incremental@<:@=bf|df@:>@],
			[Enable incremental algorithm [bf]])],
	[bv_cv_incremental=$enableval], [bv_cv_incremental="no"])
AC_MSG_RESULT($bv_cv_incremental)
case $bv_cv_incremental in
	yes | bf)
		AC_DEFINE(USE_INCREMENTAL_BF,[], 
			  [Use breadth-first incremental algorithm])
	;;
	df)
		AC_DEFINE(USE_INCREMENTAL_DF,[], 
			  [Use depth-first incremental algorithm])
	;;
esac

AC_ARG_WITH(ntl,
            [  --with-ntl=DIR          DIR Location of NTL],
            [ echo "Package ntl : $withval" && ntl_package=$withval],  
            [ ntl_package=yes ])

if test "$ntl_package" = "no"; then
    AC_MSG_ERROR(Need ntl)
fi

if test "$ntl_package" != "yes"; then
    CPPFLAGS="-I$ntl_package/include $CPPFLAGS"
    LDFLAGS="-L$ntl_package/lib $LDFLAGS"
fi

AC_EGREP_CPP(yes, [
    #include <NTL/ZZ.h>
    #ifdef NTL_GMP_LIP
    yes
    #endif
    ],:,AC_MSG_ERROR(ntl not compiled with gmp support))

AC_CHECK_LIB(ntl, main,[],[
    AC_MSG_ERROR(Need ntl)
])

AC_SUBST(GIT_HEAD_ID)
AC_SUBST(GIT_HEAD)
AC_SUBST(GIT_HEAD_VERSION)
if test -f $srcdir/.git/HEAD; then
    GIT_HEAD="$srcdir/.git/HEAD"
    GIT_HEAD_ID=`cat $srcdir/.git/HEAD`
elif test -f $srcdir/GIT_HEAD_ID; then
    GIT_HEAD_ID=`cat $srcdir/GIT_HEAD_ID`
else
    mysrcdir=`(cd $srcdir; pwd)`
    head=`basename $mysrcdir | sed -e 's/.*-//'`
    head2=`echo $head | sed -e 's/[^0-9a-f]//'`
    head3=`echo $head2 | sed -e 's/........................................//'`
    if test "x$head3" = "x" -a "x$head" = "x$head2"; then
	GIT_HEAD_ID="$head"
    else
	GIT_HEAD_ID="UNKNOWN"
    fi
fi
if test -z "$GIT_HEAD" ; then
    GIT_HEAD_VERSION="$GIT_HEAD_ID"
else
    GIT_HEAD_VERSION='`cat $<`'
fi
echo '#define GIT_HEAD_ID "'$GIT_HEAD_ID'"' > version.h

AX_CREATE_PKGCONFIG_INFO

AC_CONFIG_HEADERS(config.h)
AC_OUTPUT(Makefile omega/Makefile)
